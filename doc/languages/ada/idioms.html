<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns='http://www.w3.org/1999/xhtml' xml:lang='en'>
	<head>
		<meta http-equiv="Content-Type" content="text/html; charset=ISO-8859-1" />
		<title>SOCI-Ada - documentation</title>
		<link rel="stylesheet" type="text/css" href="style.css" />
	</head>
<body>
<h1>SOCI-Ada - manual</h1>

<p class="section"><b>Common Idioms</b></p>
<p>
As any other library, SOCI-Ada has its set of idioms that ensure optimal work in terms of performance and resource usage. Still, the optimal use will depend on the concrete usage scenario - the places where programmer choices are needed will be described explicitly.
</p>
<p>
The idioms below are provided as <i>complete programs</i> with the intent to make them more understandable and to give complete context of use for each idiom. The programs assume that the target database is PostgreSQL, but this can be changed by a different connection string in each place where the sessions are established. The programs use the Ada 2005 interface and some minor changes will be required to adapt them for Ada 95 compilers.
</p>

<p class="section"><b>Single query without data transfer</b></p>
<p>
This type of query is useful for DDL commands and can be executed directly on the given session, without explicit statement management.
</p>
<pre class="example">
with SOCI;

procedure My_Program is

   SQL : SOCI.Session := SOCI.Make_Session ("postgresql://dbname=my_database");

begin

   SQL.Execute ("drop table some_table");

end My_Program;
</pre>

<div class="note">
<p><span class="note">Note:</span></p>
<p>
The session object is initialized by a constructor function call. An alternative would be to declare it without initialization and later use the <code>Open</code> operation to establish a physical connection with the database.
</p>
</div>

<p class="section"><b>Simple query without parameters resulting in one row of data</b></p>
<p>
This type of query requires only single into elements, which together with the statement have to be manipulated explicitly.
</p>
<pre class="example">
with SOCI;
with Ada.Text_IO;

procedure My_Program is

   SQL : SOCI.Session := SOCI.Make_Session ("postgresql://dbname=my_database");
   St : SOCI.Statement := SOCI.Make_Statement (SQL);
   Pos : SOCI.Into_Position;

   Num_Of_Persons : SOCI.DB_Integer;

begin

   Pos := St.Into_Integer;
   St.Prepare ("select count(*) from persons");
   St.Execute (True);

   Num_Of_Persons := St.Get_Into_Integer (Pos);

   Ada.Text_IO.Put_Line ("Number of persons: " &amp; SOCI.DB_Integer'Image (Num_Of_Persons));

end My_Program;
</pre>

<div class="note">
<p><span class="note">Note:</span></p>
<p>
The into element is inspected by providing the position value that was obtained at the time if was created. No operations are defined for the position type. There can be many into elements with a single query.
</p>
</div>

<p class="section"><b>Simple query with parameters and without results</b></p>
<p>
This type of query requires only use elements.
</p>
<pre class="example">
with SOCI;

procedure My_Program is

   SQL : SOCI.Session := SOCI.Make_Session ("postgresql://dbname=my_database");
   St : SOCI.Statement := SOCI.Make_Statement (SQL);

begin

   St.Use_Integer ("increase");
   St.Set_Use_Integer ("increase", 1000);

   St.Prepare ("update persons set salary = salary + :increase");
   St.Execute (True);

end My_Program;
</pre>

<div class="note">
<p><span class="note">Note:</span></p>
<p>
The "<code>:increase</code>" in the query is a placeholder variable. There can be many such variables and each of them needs to be filled in by respective use element.
</p>
</div>

<p class="section"><b>Repeated query with parameters and without results</b></p>
<p>
This type of query requires only use elements, but they can be set differently for each statement execution.
</p>
<pre class="example">
with SOCI;

procedure My_Program is

   SQL : SOCI.Session := SOCI.Make_Session ("postgresql://dbname=my_database");
   St : SOCI.Statement := SOCI.Make_Statement (SQL);

begin

   St.Use_String ("name");

   St.Prepare ("insert into countries(country_name) values(:name)");

   St.Set_Use_String ("name", "Poland");
   St.Execute (True);

   St.Set_Use_String ("name", "Switzerland");
   St.Execute (True);

   St.Set_Use_String ("name", "France");
   St.Execute (True);

end My_Program;
</pre>

<div class="note">
<p><span class="note">Note:</span></p>
<p>
Each time the query is executed, the <i>current</i> values of use elements are transferred to the database.
</p>
</div>

<p class="section"><b>Batch query with parameters and without results</b></p>
<p>
This type of query requires vector use elements. Compare with the previous example.
</p>
<pre class="example">
with SOCI;

procedure My_Program is

   SQL : SOCI.Session := SOCI.Make_Session ("postgresql://dbname=my_database");
   St : SOCI.Statement := SOCI.Make_Statement (SQL);
   First : SOCI.Vector_Index;

   use type SOCI.Vector_Index;

begin

   St.Use_Vector_String ("name");

   St.Use_Vectors_Resize (3);

   First := St.Use_Vectors_First_Index;

   St.Set_Use_Vector_String ("name", First + 0, "Poland");
   St.Set_Use_Vector_String ("name", First + 1, "Switzerland");
   St.Set_Use_Vector_String ("name", First + 2, "France");

   St.Prepare ("insert into countries(country_name) values(:name)");
   St.Execute (True);

end My_Program;
</pre>

<div class="note">
<p><span class="note">Note:</span></p>
<p>
The whole bunch of data is transferred to the database if the target database server supports it and the statement is automatically repeated otherwise. This is the preferred way to transfer many rows of data to the server when the data for all rows are known before executing the query.
</p>
</div>

<div class="note">
<p><span class="note">Note:</span></p>
<p>
The query can be executed many times and each time a new batch of data can be transferred to the server. The size of the batch (set by calling <code>Use_Vectors_Resize</code>) can be different each time the query is executed, but cannot be larger than the size that was used the first time. The size of the batch defines a tradeoff between the amount of data being transmitted in a single step (this influences the memory used by the user program and the time of a single call) and the number of executions required to handle big data sets. The optimal size of the batch will therefore differ depending on the application, but in general tens of thousands is a reasonable limit for a batch size - the performance of the whole operation is usually not affected above this value so there is no need to imply higher memory usage at the client side.
</p>
</div>

<p class="section"><b>Simple query with many rows of results</b></p>
<p>
This type of query requires simple into elements.
</p>
<pre class="example">
with SOCI;
with Ada.Text_IO;

procedure My_Program is

   SQL : SOCI.Session := SOCI.Make_Session ("postgresql://dbname=my_database");
   St : SOCI.Statement := SOCI.Make_Statement (SQL);
   Pos : SOCI.Into_Position;

begin

   Pos := St.Into_String;

   St.Prepare ("select country_name from countries");
   St.Execute;

   while St.Fetch loop

      Ada.Text_IO.Put_Line (St.Get_Into_String (Pos));

   end loop;

end My_Program;
</pre>

<div class="note">
<p><span class="note">Note:</span></p>
<p>
The loop above executes as many times as there are rows in the result. After each row is read, the into elements contain the respective values from that row. The <code>Execute</code> operation is called without parameter, which is <code>False</code> by default, meaning that no data transfer is intended. The data is being transferred only during the <code>Fetch</code> operation, which returns <code>False</code> when no data has been retrieved and the result is exhausted.
</p>
<p>
This type of query can have simple parameters which are fixed at the execution time.
</p>
</div>

<p class="section"><b>Batch query with many rows of results</b></p>
<p>
This type of query requires vector into elements. Compare with previous example.
</p>
<pre class="example">
with SOCI;
with Ada.Text_IO;

procedure My_Program is

   SQL : SOCI.Session := SOCI.Make_Session ("postgresql://dbname=my_database");
   St : SOCI.Statement := SOCI.Make_Statement (SQL);
   Pos : SOCI.Into_Position;

   Batch_Size : constant := 10;

begin

   Pos := St.Into_Vector_String;
   St.Into_Vectors_Resize (Batch_Size);

   St.Prepare ("select country_name from countries");
   St.Execute;

   while St.Fetch loop

      for I in St.Into_Vectors_First_Index .. St.Into_Vectors_Last_Index loop

         Ada.Text_IO.Put_Line (St.Get_Into_Vector_String (Pos, I));

      end loop;

      St.Into_Vectors_Resize (Batch_Size);

   end loop;

end My_Program;
</pre>

<div class="note">
<p><span class="note">Note:</span></p>
<p>
The loop above is nested. The outer <code>while</code> loop fetches consecutive batches of rows from the database with requested batch size; the returned batch can be smaller than requested (the into vector elements are downsized automatically if needed) and the intended batch size is requested again before repeating the <code>Fetch</code> operation. For each returned batch, the into vector elements are inspected in the inner <code>for</code> loop. This scheme ensures correct operation independently on the size of returned batch and is therefore a recommended idiom for efficiently returning many rows of data.
</p>
<p>
There is a tradeoff between efficiency and memory usage and this tradeoff is controlled by the requested batch size. Similarly to one of the examples above, there is no benefit from using batches bigger than tens of thousands of rows.
</p>
<p>
This type of query can have simple (not vectors) parameters that are fixed at execution time.
</p>
</div>

<div class="note">
<p><span class="note">Final note:</span></p>
<p>
Follow good database usage principles and avoid constructing queries by concatenating strings computed at run-time. Thanks to a good type system Ada is much better in preventing various SQL-injection attacks than weaker languages like PHP, but there is still a potential for vulnerability or at least performance loss. As a rule of thumb, rely on <i>use elements</i> to parameterize your queries and to provide clean separation between data and code. This will prevent many security vulnerabilities and will allow some servers to optimize their work by reusing already cached execution plans.
</p>
</div>

<table class="foot-links" border="0" cellpadding="2" cellspacing="2">
  <tr>
    <td class="foot-link-left">
      <a href="concepts.html">Concepts</a>
    </td>
    <td class="foot-link-right">
      <a href="reference.html">API Reference</a>
    </td>
  </tr>
</table>

<p class="copyright">Copyright &copy; 2008-2011 Maciej Sobczak</p>
</body>
</html>
